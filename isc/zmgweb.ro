IRIS for Windows^MAC^mg-web back-end routines^~Format=IRIS.S~^RAW
%RO on 03 Dec 2020   4:44 PM
%zmgweb^MAC^^65716,60263.112524^0
%zmgweb ; mg-web M back-end main interface
 ;
 ;----------------------------------------------------------------------------
 ;| zmgweb: mg-web Back-end Support Routines                                 |
 ;|                                                                          |
 ;| Copyright (c) 2023 MGateway Ltd,                                         |
 ;| Redhill, Surrey UK.                                                      |
 ;| All rights reserved.                                                     |
 ;|                                                                          |
 ;| https://www.mgateway.com                                                 |
 ;| Email: rtweed@mgateway.com                                               |
 ;|                                                                          |
 ;|                                                                          |
 ;| Licensed under the Apache License, Version 2.0 (the "License");          |
 ;| you may not use this file except in compliance with the License.         |
 ;| You may obtain a copy of the License at                                  |
 ;|                                                                          |
 ;|     http://www.apache.org/licenses/LICENSE-2.0                           |
 ;|                                                                          |
 ;| Unless required by applicable law or agreed to in writing, software      |
 ;| distributed under the License is distributed on an "AS IS" BASIS,        |
 ;| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. |
 ;| See the License for the specific language governing permissions and      |
 ;|  limitations under the License.                                          |
 ;----------------------------------------------------------------------------
 ;
 ; 3 December 2020
 ;
 QUIT
 ;
ok() ;
 QUIT 1
 ;
ylink ;
 i $$ok^%zmgwebUtils()
 i $$ok^%zmgwebJWT()
 i $$ok^%zmgwebCfg()
 QUIT
 ;
.
api(%cgi,%var,%sys) ; mg_web handler for URLs matching /api/*
 ;
 n call,req
 ;
 ;m ^trace($h,"cgi")=%cgi
 ;m ^trace($h,"var")=%var
 ;m ^trace($h,"sys")=%sys
 ;
 i $$parseRequest(.%cgi,.%var,.req)
 ;
 ; Match against the Method & URI path to identify handler
 ;
 s call=""
 i req("method")'="",req("path")'="" d
 . ; Check for an exact uri and path match first (e.g. "/static/rest/path")
 . s call=$g(^%zmgweb("routes",req("method"),req("path"))) i call'="" d  q
 . . s req("path_template")=req("path")
 . ; Oherwise search for matches with cariables in path (e.g. "/api/:variable/action")
 . ;
 . n i,match,plen,p1,u1,uri
 . ;
 . s match=0
 . s plen=$l(req("path"),"/")
 . s uri="" f  s uri=$o(^%zmgweb("routes",req("method"),uri),-1) q:uri=""  d  q:call'=""
 . . i plen'=$l(uri,"/") q
 . . f i=2:1 s u1=$p(uri,"/",i),p1=$p(req("path"),"/",i) q:(u1=""&(p1=""))  d  q:'match
 . . . i u1=p1 s match=1 q
 . . . i $e(u1)=":" d  q
 . . . . s match=1
 . . . . s req("params",$p(u1,":",2))=p1
 . . . s match=0 q
 . . i match d
 . . . s call=^%zmgweb("routes",req("method"),uri)
 . . . s req("path_template")=uri
 ;
 i call="" q $$notFound()
 s req("call")=call
 i $e(call,1,6)="class(" d
 . s call="##"_call_"(.req)"
 e  d
 . s call="$$"_call_"(.req)"
 ;m ^trace($h,"req")=req
 QUIT @call
 ;
response(res,headers) ;
 QUIT $$response^%zmgwebUtils(.res,.headers)
 ;
notFound() ;
  ;
  n errors
  ;
  s errors("error")="Resource not found"
  QUIT $$errorResponse(.errors,404)
  ;
errorResponse(errors,statusCode,statusText) ;
  ;
  n crlf,header,json,status
  ;
  i '$d(^%zmgweb("errorCodes")) d
  . s ^%zmgweb("errorCodes",400)="Bad Request"
  . s ^%zmgweb("errorCodes",401)="unauthorized"
  . s ^%zmgweb("errorCodes",402)="Payment Required"
  . s ^%zmgweb("errorCodes",403)="Forbidden"
  . s ^%zmgweb("errorCodes",404)="Not Found"
  . s ^%zmgweb("errorCodes",405)="Method Not Allowed"
  . s ^%zmgweb("errorCodes",406)="Not Acceptable"
  . s ^%zmgweb("errorCodes",407)="Proxy Authentication Required"
  . s ^%zmgweb("errorCodes",408)="Request Timeout"
  . s ^%zmgweb("errorCodes",409)="Conflict"
  . s ^%zmgweb("errorCodes",410)="Gone"
  . s ^%zmgweb("errorCodes",411)="Length Required"
  . s ^%zmgweb("errorCodes",412)="Precondition Failed"
  . s ^%zmgweb("errorCodes",413)="Payload Too Large"
  . s ^%zmgweb("errorCodes",414)="URI Too Long"
  . s ^%zmgweb("errorCodes",415)="Unsupported Media Type"
  . s ^%zmgweb("errorCodes",416)="Range Not Satisfiable"
  . s ^%zmgweb("errorCodes",417)="Expectation Failed"
  . s ^%zmgweb("errorCodes",418)="I'm a teapot"
  . s ^%zmgweb("errorCodes",421)="Misdirected Request"
  . s ^%zmgweb("errorCodes",422)="Unprocessable Entity"
  . s ^%zmgweb("errorCodes",423)="Locked"
  . s ^%zmgweb("errorCodes",424)="Failed Dependency"
  . s ^%zmgweb("errorCodes",425)="Too Early"
  . s ^%zmgweb("errorCodes",426)="Upgrade Required"
  . s ^%zmgweb("errorCodes",428)="Precondition Required"
  . s ^%zmgweb("errorCodes",429)="Too Many Requests"
  . s ^%zmgweb("errorCodes",431)="Request Header Fields Too Large"
  . s ^%zmgweb("errorCodes",451)="Unavailable For Legal Reasons"
  . s ^%zmgweb("errorCodes",500)="Internal Server Error"
  . s ^%zmgweb("errorCodes",501)="Not Implemented"
  . s ^%zmgweb("errorCodes",502)="Bad Gateway"
  . s ^%zmgweb("errorCodes",503)="Service Unavailable"
  . s ^%zmgweb("errorCodes",504)="Gateway Timeout"
  . s ^%zmgweb("errorCodes",505)="HTTP Version Not Supported"
  . s ^%zmgweb("errorCodes",506)="Variant Also Negotiates"
  . s ^%zmgweb("errorCodes",507)="Insufficient Storage"
  . s ^%zmgweb("errorCodes",508)="Loop Detected"
  . s ^%zmgweb("errorCodes",510)="Not Extended"
  . s ^%zmgweb("errorCodes",511)="Network Authentication Required"
  ;
  i '$d(statusCode) s statusCode=422
  i '$d(statusText) s statusText=$g(^%zmgweb("errorCodes",statusCode))
  i '$d(statusText) s statusText="Unprocessable Entity"
  s json=$$arrayToJSON^%zmgwebUtils("errors")
  s crlf=$c(13,10)
  s header="HTTP/1.1 "_statusCode_" "_statusText_crlf
  s header=header_"Content-type: application/json"_crlf_crlf
  QUIT header_json
  ;
parseRequest(%cgi,%var,req) ;
  ;
  n key,nvps,qs
  ;
  k req
  ;
  s qs=$g(%cgi("QUERY_STRING"))
  i qs'="" d
  . n i,noOfNvps
  . s noOfNvps=$l(qs,"&")
  . f i=1:1:noOfNvps d
  . . n n,nvp
  . . s nvp=$p(qs,"&",i)
  . . s n=$p(nvp,"=",1)
  . . s nvps(n)=$p(nvp,"=",2,1000)
  . m req("query")=nvps
  s key="HTTP"
  f  s key=$o(%cgi(key)) q:key=""  q:key'["HTTP_"  d
  . n k
  . s k=$p(key,"HTTP_",2)
  . s k=$zconvert(k,"L")
  . s req("headers",k)=%cgi(key)
  s req("method")=$g(%cgi("REQUEST_METHOD"))
  s req("path")=$g(%cgi("SCRIPT_NAME"))
  ;
  i $g(%var)'="" d
  . n crlf,payload
  . s crlf=$c(13,10)
  . s payload=$$replace^%zmgwebUtils(%var,crlf,"")
  . i $$parseJSON^%zmgwebUtils(payload,.json)
  . m req("body")=json
  ;
  QUIT 1
  ;
header(headers)  ;
  ;
  n crlf,header
  ;
  s crlf=$c(13,10)
  s header="HTTP/1.1 200 OK"_crlf
  s header=header_"Content-type: application/json"_crlf
  i $d(headers) d
  . n key
  . s key=""
  . f  s key=$o(headers(key)) q:key=""  d
  . . s header=header_key_": "_headers(key)_crlf
  q header_crlf
  ;
.

%zmgwebCfg^MAC^^65709,58007.391803^0
%zmgwebCfg ; mg-web Configuration Functions
 ;
 ;----------------------------------------------------------------------------
 ;| zmgweb: mg-web Back-end Support Routines                                 |
 ;|                                                                          |
 ;| Copyright (c) 2023 MGateway Ltd,                                         |
 ;| Redhill, Surrey UK.                                                      |
 ;| All rights reserved.                                                     |
 ;|                                                                          |
 ;| https://www.mgateway.com                                                 |
 ;| Email: rtweed@mgateway.com                                               |
 ;|                                                                          |
 ;|                                                                          |
 ;| Licensed under the Apache License, Version 2.0 (the "License");          |
 ;| you may not use this file except in compliance with the License.         |
 ;| You may obtain a copy of the License at                                  |
 ;|                                                                          |
 ;|     http://www.apache.org/licenses/LICENSE-2.0                           |
 ;|                                                                          |
 ;| Unless required by applicable law or agreed to in writing, software      |
 ;| distributed under the License is distributed on an "AS IS" BASIS,        |
 ;| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. |
 ;| See the License for the specific language governing permissions and      |
 ;|  limitations under the License.                                          |
 ;----------------------------------------------------------------------------
 ;
 ; 26 November 2020
 ;
 QUIT
 ;
ok() ;
 QUIT 1
 ;
build(filename,mgwebConfPath) ;
 ;
 n config,ok
 ;
 s ok=0
 i $g(filename)="" s filename="/opt/mgweb/mapped/mgweb.conf.json"
 i $$readJSONFile^%zmgwebUtils(filename,.config) d
 . n opened
 . ;
 . i $g(mgwebConfPath)="" s mgwebConfPath="/opt/mgweb/mgweb.conf"
 . i $$deleteFile^%zmgwebUtils(mgwebConfPath)
 . s opened=$$openNewFile^%zmgwebUtils(mgwebConfPath)
 . i opened d
 . . n io,name,value
 . . s io=$io
 . . u mgwebConfPath
 . . s name=""
 . . f  s name=$o(config(name)) q:name=""  d
 . . . ;
 . . . i name="cgi" d  q
 . . . . ; add <cgi> section
 . . . . w "<cgi>",!
 . . . . n no
 . . . . s no=""
 . . . . f  s no=$o(config(name,no)) q:no=""  d
 . . . . . w "  "_config(name,no),!
 . . . . w "</cgi>",!
 . . . ;
 . . . i name="servers" d  q
 . . . . ; add <server> sections
 . . . . n param,serverName
 . . . . s serverName=""
 . . . . f  s serverName=$o(config(name,serverName)) q:serverName=""  d
 . . . . . w "<server "_serverName_">",!
 . . . . . s param=""
 . . . . . f  s param=$o(config(name,serverName,param)) q:param=""  d
 . . . . . . i param="env" d  q
 . . . . . . . ; YottaDB <env> sub-section
 . . . . . . . n envName
 . . . . . . . w "  <env>",!
 . . . . . . . s envName=""
 . . . . . . . f  s envName=$o(config(name,serverName,param,envName)) q:envName=""  d
 . . . . . . . . w "    "_envName_"="_config(name,serverName,param,envName),!
 . . . . . . . w "  </env>",!
 . . . . . . w "  "_param_" "_config(name,serverName,param),!
 . . . . . w "</server>",!
 . . . ;
 . . . i name="locations" d  q
 . . . . ; add <location> sections
 . . . . n uri
 . . . . s uri=""
 . . . . f  s uri=$o(config(name,uri)) q:uri=""  d
 . . . . . n param
 . . . . . w "<location "_uri_">",!
 . . . . . s param=""
 . . . . . f  s param=$o(config(name,uri,param)) q:param=""  d
 . . . . . . i param="server" d  q
 . . . . . . . w "  servers "_config(name,uri,param),!
 . . . . . . i param="servers" d  q
 . . . . . . . n no
 . . . . . . . w "  servers"
 . . . . . . . s no=""
 . . . . . . . f  s no=$o(config(name,uri,param,no)) q:no=""  d
 . . . . . . . . w " "_config(name,uri,param,no)
 . . . . . . . w !
 . . . . . . w "  "_param_" "_config(name,uri,param),!
 . . . . . w "</location>",!
 . . . w name_" "_config(name),!
 . . ;
 . . c mgwebConfPath
 . . u io
 . . s ok=1
 . ;
 QUIT ok
 ;
.

%zmgwebJWT^MAC^^65716,53447.773555^0
%zmgwebJWT ; mg-web M back-end: JSON Web Token Functions
 ;
 ;----------------------------------------------------------------------------
 ;| zmgweb: mg-web Back-end Support Routines                                 |
 ;|                                                                          |
 ;| Copyright (c) 2023 MGateway Ltd,                                         |
 ;| Redhill, Surrey UK.                                                      |
 ;| All rights reserved.                                                     |
 ;|                                                                          |
 ;| https://www.mgateway.com                                                 |
 ;| Email: rtweed@mgateway.com                                               |
 ;|                                                                          |
 ;|                                                                          |
 ;| Licensed under the Apache License, Version 2.0 (the "License");          |
 ;| you may not use this file except in compliance with the License.         |
 ;| You may obtain a copy of the License at                                  |
 ;|                                                                          |
 ;|     http://www.apache.org/licenses/LICENSE-2.0                           |
 ;|                                                                          |
 ;| Unless required by applicable law or agreed to in writing, software      |
 ;| distributed under the License is distributed on an "AS IS" BASIS,        |
 ;| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. |
 ;| See the License for the specific language governing permissions and      |
 ;|  limitations under the License.                                          |
 ;----------------------------------------------------------------------------
 ;
 ; 3 December 2020
 ;
 QUIT
 ;
ok() ;
 QUIT 1
 ;
test1() ;
 n data,jwt,jwtSecret,payload
 s payload("userId")="b08f86af-35da-48f2-8fab-cef3904660bd"
 s jwt=$$createJWT(.payload,600)
 QUIT jwt
 ;
getJWTSecret() ;
 i '$d(^%zmgweb("jwt","secret")) d
 . s ^%zmgweb("jwt","secret")=$$createJWTUid()
 QUIT ^%zmgweb("jwt","secret")
 ;
setIssuer(issuer) ;
 ;
 s ^%zmgweb("jwt","iss")=$g(issuer)
 QUIT 1
 ;
getIssuer() ;
 ;
 n iss
 ;
 s iss=$g(^%zmgweb("jwt","iss"))
 i iss="" d
 . s iss="mg_web"
 . i $$setIssuer(iss)
 QUIT iss
 ;
createJWT(payload,expiry,jwtSecret)
 n data,hashedData,header
 ;
 i '$d(payload("iss")) s payload("iss")=$$getIssuer()
 i $g(expiry) s payload("exp")=$$getExpiryTime(expiry)
 ;
 i $g(jwtSecret)="" s jwtSecret=$$getJWTSecret()
 s header=$$base64UrlEncode($$jwtHeader())
 s data=$$arrayToJSON^%zmgwebUtils("payload")
 s data=$$base64UrlEncode(data)
 s data=header_"."_data
 QUIT data_"."_$$hash(data,jwtSecret)
 ;
decodeJWT(jwt) ;
 n hashedPayload,payload
 ;
 s hashedPayload=$p(jwt,".",2)
 s payload=$$base64UrlDecode(hashedPayload) 
 QUIT payload
 ;
authenticateJWT(jwt,jwtSecret,failReason) ;
 ;
 n data,exp,hashedData,isValid,signature,thisSignature
 ;
 i $g(jwtSecret)="" s jwtSecret=$$getJWTSecret()
 s failReason=""
 s data=$p(jwt,".",1,2)
 s signature=$p(jwt,".",3)
 s hashedData=$$hash(data,jwtSecret)
 i signature'=hashedData d  QUIT 0
 . s failReason="Invalid signature"
 s isValid=1
 s exp=$$getClaim(jwt,"exp")
 i exp'="",$$isExpired(exp) d
 . s isValid=0
 . s failReason="JWT has expired" 
 QUIT isValid
 ;
getClaims(jwt,claims) ;
 ;
 n json
 ;
 k claims
 s json=$$decodeJWT(jwt)
 QUIT $$parseJSON^%zmgwebUtils(json,.claims)
 ;
getClaim(jwt,claim) ;
 n arr,claims,json
 ;
 s json=$$decodeJWT(jwt)
 i $$parseJSON^%zmgwebUtils(json,.claims)
 QUIT $g(claims(claim))
 ;
createJWTUid()
 n i,string,token
 ;
 s string="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz1234567890-"
 s token=""
 f i=1:1:50 s token=token_$e(string,($r($l(string))+1))
 s token=$$replace^%zmgwebUtils(token,"--","-")
 QUIT token
 ; 
base64UrlEncode(input)
 ;
 n hash,stop
 ;
 i $zv["GT.M" d
 . n command,io,name,str
 . s io=$io
 . s name="base64"
 . s command="base64 -w 0 -i -"
 . o name:(command=command)::"pipe"
 . use name w input,/EOF 
 . read hash 
 . u $io close name
 e  d
 . s hash=$System.Encryption.Base64Encode(input)
 ;
 ; remove trailing encoded line feed
 s hash=$reverse(hash)
 i $e(hash,1,4)="==gC" d
 . s hash=$e(hash,5,$l(hash))
 s hash=$reverse(hash)
 ; remove any trailing = characters
 s stop=0
 f  q:stop  d
 . i $e(hash,$l(hash))="=" d
 . . s hash=$e(hash,1,$l(hash)-1)
 . e  d
 .. s stop=1
 s hash=$$replace^%zmgwebUtils(hash,$c(13),"")
 s hash=$$replace^%zmgwebUtils(hash,$c(10),"")
 s hash=$$replace^%zmgwebUtils(hash,"+","-")
 s hash=$$replace^%zmgwebUtils(hash,"/","_")
 QUIT hash
 ;
base64UrlDecode(hash) ;
 n io,rem,string
 ;
 s io=$io
 s hash=$$replace^%zmgwebUtils(hash,"-","+")
 s hash=$$replace^%zmgwebUtils(hash,"_","/")
 s rem=$l(hash)#4
 i rem=2 s hash=hash_"=="
 i rem=3 s hash=hash_"="
 i $zv["GT.M" d
 . n command,name
 . s name="base64"
 . s command="base64 -d -"
 . open name:(command=command)::"pipe"
 . u name w hash,/EOF
 . read string
 . u io close name
 e  d
 . s string=$System.Encryption.Base64Decode(hash)
 ;
 QUIT string
 ;
hash(data,secretKey) ;
 ;
 n hash,stop
 ;
 i $zv["GT.M" d
 . n command,io,name
 . s name="hmac"
 . s io=$io
 . ;s command="echo -n '"_data_"' | openssl dgst -sha256 -hmac '"_secretKey_"' -binary | base64"
 . s command="openssl dgst -sha256 -hmac '"_secretKey_"' | base64"
 . open name:(command=command)::"pipe"
 . use name w data,/EOF 
 . read hash 
 . u io close name
 e  d
 . s hash=$System.Encryption.HMACSHA(256,data,secretKey)
 . s hash=$System.Encryption.Base64Encode(hash)
 ;
 s stop=0
 f  q:stop  d
 . i $e(hash,$l(hash))="=" d
 . . s hash=$e(hash,1,$l(hash)-1)
 . e  d
 . . s stop=1
 s hash=$$replace^%zmgwebUtils(hash,$c(13),"")
 s hash=$$replace^%zmgwebUtils(hash,$c(10),"")
 s hash=$$replace^%zmgwebUtils(hash,"+","-")
 s hash=$$replace^%zmgwebUtils(hash,"/","_")
 ;
 QUIT hash
 ;
jwtHeader() ;
 QUIT "{""typ"":""JWT"",""alg"":""HS256""}"
 ;
isExpired(exp) ;
 ;
 n exph,now
 ;
 s exph=exp+4070908800
 s now=$$convertDateToSeconds^%zmgwebUtils($h)
 QUIT (now>exph)
 ;
getExpiryTime(expiry) ;
 ;
 n exp,now,then
 ;
 s now=$$convertDateToSeconds^%zmgwebUtils($h)
 s then=now+expiry
 s exp=then-4070908800
 QUIT exp
 ;
.

%zmgwebUtils^MAC^^65715,55151.537429^0
%zmgwebUtils ; mg-web M back-end utilities
 ;
 ;----------------------------------------------------------------------------
 ;| zmgweb: mg-web Back-end Support Routines                                 |
 ;|                                                                          |
 ;| Copyright (c) 2023 MGateway Ltd,                                         |
 ;| Redhill, Surrey UK.                                                      |
 ;| All rights reserved.                                                     |
 ;|                                                                          |
 ;| https://www.mgateway.com                                                 |
 ;| Email: rtweed@mgateway.com                                               |
 ;|                                                                          |
 ;|                                                                          |
 ;| Licensed under the Apache License, Version 2.0 (the "License");          |
 ;| you may not use this file except in compliance with the License.         |
 ;| You may obtain a copy of the License at                                  |
 ;|                                                                          |
 ;|     http://www.apache.org/licenses/LICENSE-2.0                           |
 ;|                                                                          |
 ;| Unless required by applicable law or agreed to in writing, software      |
 ;| distributed under the License is distributed on an "AS IS" BASIS,        |
 ;| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. |
 ;| See the License for the specific language governing permissions and      |
 ;|  limitations under the License.                                          |
 ;----------------------------------------------------------------------------
 ;
 ; 2 December 2020
 ;
 QUIT
 ;
ok() ;
 QUIT 1
 ;
start ;
 d start^%zmgsi(0)
 w "%zmgsi started",!
 i $$ok^%zmgweb()
 i $$getJWTSecret^%zmgwebJWT()
 i $$setJWTIssuer()
 i $$buildAPIs()
 i $$build^%zmgwebCfg()
 QUIT
 ;
reconfigure ;
 i $$build^%zmgwebCfg()
 QUIT
 ;
buildRoutes ;
 i $$buildAPIs()
 QUIT
 ;
response(res,headers) ;
 QUIT $$header^%zmgweb(.headers)_$$arrayToJSON("res")
 ;
setJWTIssuer(filename)
 ;
 n config,ok
 ;
 s ok=0
 i $g(filename)="" s filename="/opt/mgweb/mapped/config.json"
 i $$readJSONFile(filename,.config) d
 . s ^%zmgweb("jwt","iss")=$g(config("jwt","iss"))
 . s ok=1
 QUIT ok
 ;
buildAPIs(filename) ;
 ;
 n i,ok,routes
 ;
 k ^%zmgweb("routes")
 ;
 i $g(filename)="" s filename="/opt/mgweb/mapped/routes.json"
 s ok=$$readJSONFile(filename,.routes)
 i 'ok QUIT ok
 s i=""
 f  s i=$o(routes(i)) q:i=""  d
 . s ^%zmgweb("routes",routes(i,"method"),routes(i,"uri"))=routes(i,"handler")
 ;
 QUIT 1
  ;
readJSONFile(filename,array) ;
 ;
 n io,json,line,ok,%zt
 ;
 k array
 i $zv'["GT.M" d  QUIT ok
 . ; equivalent logic for Cache/IRIS...
 . ;
 . s io=$io
 . s ok=$$openFile(filename)
 . q:'ok
 . s %zt=$zt
 . s $zt="eoReadJSONFile"
 . s json=""
 . f  u filename r line g:$zeof eoReadJSONFile s json=json_line
 . QUIT
 ;
 s io=$io
 s ok=$$openFile(filename)
 i 'ok QUIT 0
 s json=""
 u filename:exception="goto eoReadJSONFile"
 f  u filename r line s json=json_line
 ;
eoReadJSONFile ;
 c filename u io
 i $zv'["GT.M" s $zt=%zt
 s json=$tr(json,$c(13),"")
 i $$parseJSON(json,.array)
 QUIT 1
  ;
deleteFile(filepath)
 i $zv'["GT.M" QUIT $zu(140,5,filepath)
 n status
 d gtmDeleteFile(filepath)
 QUIT status
 ;
gtmDeleteFile(filepath)
 s status=1
 o filepath:(readonly:exception="g deleteNotExists") 
 c filepath:DELETE
 QUIT
 ;
deleteNotExists
 s status=0
 QUIT
 ;
openNewFile(filepath)
 n ok
 s ok=0
 i $zv["GT.M" d  QUIT ok
 . o filepath:(noreadonly:variable:newversion:exception="g openNewFileNotExists")
 . s ok=1 
 e  d  QUIT ok
 . o filepath:"rw" i  s ok=1
 QUIT 1
 ;
openNewFileNotExists
 QUIT
 ;
openFile(filepath)
 n ok
 i $zv["GT.M" d  QUIT ok
 . s ok=0
 . o filepath:(readonly:exception="g openFileNotExists")
 . s ok=1
 e  d  QUIT ok
 . s ok=1
 . o filepath:"r":0 e  s ok=0
 QUIT 1
 ;
openFileNotExists
 s $zt=""
 QUIT
 ;
arrayToJSON(name)
 n subscripts
 i '$d(@name) QUIT "[]"
 QUIT $$walkArray("",name)
 ;
walkArray(json,name,subscripts)
 ;
 n allNumeric,arrComma,brace,comma,count,cr,dd,i,no,numsub,dblquot,quot
 n ref,sub,subNo,subscripts1,type,valquot,value,xref
 ;
 s cr=$c(13,10),comma=","
 s (dblquot,valquot)=""""
 s dd=$d(@name)
 i dd=1!(dd=11) d  i dd=1 QUIT json
 . s value=@name
 . i value'[">" q
 . s json=$$walkArray(json,value,.subscripts)
 s ref=name_"("
 s no=$o(subscripts(""),-1)
 i no>0 f i=1:1:no d
 . s quot=""""
 . i subscripts(i)?."-"1N.N s quot=""
 . s ref=ref_quot_subscripts(i)_quot_","
 s ref=ref_"sub)"
 s sub="",numsub=0,subNo=0,count=0
 s allNumeric=1
 f  s sub=$o(@ref) q:sub=""  d  q:'allNumeric
 . i sub'?1N.N s allNumeric=0
 . s count=count+1
 . i sub=0,count=1 s count=0 ; array may be zero numbered
 . i sub'=count s allNumeric=0
 i allNumeric d
 . s json=json_"["
 e  d
 . s json=json_"{"
 s sub=""
 f  s sub=$o(@ref) q:sub=""  d
 . s subscripts(no+1)=sub
 . s subNo=subNo+1
 . s dd=$d(@ref)
 . i dd=1 d
 . . s value=@ref 
 . . i 'allNumeric d
 . . . s json=json_""""_sub_""":"
 . . s type="literal"
 . . i $$numeric(value) s type="numeric"
 . . i value="true"!(value="false") s type="boolean"
 . . i $e(value,1)="{",$e(value,$l(value))="}" s type="variable"
 . . i $e(value,1,4)="<?= ",$e(value,$l(value)-2,$l(value))=" ?>" d
 . . . s type="variable"
 . . . s value=$e(value,5,$l(value)-3)
 . . i type="literal" s value=valquot_value_valquot
 . . d
 . . . ; Mike Clayton fix for numeric 0.x values: 17 April 2017
 . . . i type="numeric",$e(value,1)="." s value="0"_value
 . . . ; end Mike Clayton fix
 . . . s json=json_value_","
 . k subscripts1
 . m subscripts1=subscripts
 . i dd>9 d
 . . i sub?1N.N,allNumeric d
 . . . i subNo=1 d
 . . . . s numsub=1
 . . . . s json=$e(json,1,$l(json)-1)
 . . . . s json=json_"["
 . . e  d
 . . . s json=json_""""_sub_""":"
 . . s json=$$walkArray(json,name,.subscripts1)
 . . d
 . . . s json=json_","
 ;
 s json=$e(json,1,$l(json)-1)
 i allNumeric d
 . s json=json_"]"
 e  d
 . s json=json_"}"
 QUIT json ; exit!
 ;
numeric(value)
 i $e(value,1,9)="function(" QUIT 1
 i value?1"0."1N.N QUIT 1
 i $e(value,1)=0,$l(value)>1 QUIT 0
 i $e(value,1,2)="-0",$l(value)>2,$e(value,1,3)'="-0." QUIT 0
 i value?1N.N QUIT 1
 i value?1"-"1N.N QUIT 1
 i value?1N.N1"."1N.N QUIT 1
 i value?1"-"1N.N1"."1N.N QUIT 1
 i value?1"."1N.N QUIT 1
 i value?1"-."1N.N QUIT 1
 QUIT 0
 ;
parseJSON(jsonString,propertiesArray)
 ;
 n array,arrRef,buff,c,error
 ;
 k propertiesArray
 s error=""
 s buff=$g(jsonString)
 s buff=$$removeSpaces(buff)
 s buff=$$replace(buff,"\""","\'")
 s arrRef="array"
 s c=$e(buff,1)
 s buff=$e(buff,2,$l(buff))
 d
 . i c="{" d  q
 . . s error=$$parseJSONObject(.buff,"")
 . . q:error
 . . i buff'="" s error=1
 . i c="[" d  q
 . . s error=$$parseJSONArray(.buff,"")
 . . q:error
 . . i buff'="" s error=1
 . s error=1
 i error=1 QUIT "Invalid JSON"
 m propertiesArray=array
 QUIT ""
 ;
parseJSONObject(buff,subs)
 n c,error,name,stop,subs2,value
 s stop=0,name="",error=""
 f  d  q:stop
 . s c=$e(buff,1)
 . i c="" s error=1,stop=1 q
 . s buff=$e(buff,2,$l(buff))
 . i c="[" s error=1,stop=1 q
 . i c="}" d  q
 . . s stop=1
 . i c=":" d  q
 . . n subs2,x
 . . s value=$$getJSONValue(.buff)
 . . d  q:stop
 . . . i value="" q
 . . . i $e(value,1)="""",$e(value,$l(value))="""" q
 . . . i value="true"!(value="false") s value=""""_value_"""" q
 . . . i $$numeric(value) q
 . . . s error=1,stop=1
 . . i value="",$e(buff,1)="{" d  q
 . . . i $e(name,1)'="""",$e(name,$l(name))'="""" s name=""""_name_""""
 . . . s subs2=subs
 . . . i subs'="" s subs2=subs2_","
 . . . s subs2=subs2_name
 . . . s buff=$e(buff,2,$l(buff))
 . . . s error=$$parseJSONObject(.buff,subs2)
 . . . i error=1 s stop=1 q
 . . i value="",$e(buff,1)="[" d  q
 . . . i $e(name,1)'="""",$e(name,$l(name))'="""" s name=""""_name_""""
 . . . s subs2=subs
 . . . i subs'="" s subs2=subs2_","
 . . . s subs2=subs2_name
 . . . s buff=$e(buff,2,$l(buff))
 . . . s error=$$parseJSONArray(.buff,subs2)
 . . . i error=1 s stop=1 q
 . . i $e(name,1)="""",$e(name,$l(name))'="""" s error=1,stop=1 q
 . . i $e(name,1)'="""",$e(name,$l(name))="""" s error=1,stop=1 q
 . . i $e(name,1)'="""",$e(name,$l(name))'="""" s name=""""_name_""""
 . . s subs2=subs
 . . i subs'="" s subs2=subs2_","
 . . s subs2=subs2_name
 . . i value["\'" s value=$$replace(value,"\'","""""")
 . . s subs2=$$replace(subs2,$c(2),":")
 . . s value=$$replace(value,$c(2),":")
 . . s x="s "_arrRef_"("_subs2_")="_value
 . . x x
 . i c="," s name="" q
 . s name=name_c q
 QUIT error
 ;
parseJSONArray(buff,subs)
 n c,error,name,no,stop,subs2,value,x
 s stop=0,name="",no=0,error=""
 f  d  q:stop
 . s c=$e(buff,1)
 . i c="" s error=1,stop=1 q
 . s buff=$e(buff,2,$l(buff))
 . i c=":" d  q:stop
 . . i name'="" q
 . . s error=1,stop=1
 . i c="]" d  q
 . . s stop=1
 . . i name="" q
 . . s no=no+1
 . . s subs2=subs
 . . i subs'="" s subs2=subs2_","
 . . s subs2=subs2_no
 . . s subs2=$$replace(subs2,$c(2),":")
 . . s name=$$replace(name,$c(2),":")
 . . s x="s "_arrRef_"("_subs2_")="_name
 . . x x
 . i c="[" d  q
 . . s no=no+1
 . . s subs2=subs
 . . i subs'="" s subs2=subs2_","
 . . s subs2=subs2_no
 . . s error=$$parseJSONArray(.buff,subs2)
 . . i error=1 s stop=1 q
 . i c="{" d  q
 . . s no=no+1
 . . s subs2=subs
 . . i subs'="" s subs2=subs2_","
 . . s subs2=subs2_no
 . . s error=$$parseJSONObject(.buff,subs2)
 . . i error=1 s stop=1 q
 . s subs2=subs
 . i subs'="" s subs2=subs2_","
 . s subs2=subs2_""""_name_""""
 . i c="," d  q
 . . i name="" q
 . . d  q:stop
 . . . i $e(name,1)="""",$e(name,$l(name))="""" q
 . . . i $$numeric(name) q
 . . . s error=1,stop=1
 . . s no=no+1
 . . s subs2=subs
 . . i subs'="" s subs2=subs2_","
 . . s subs2=subs2_""""_no_""""
 . . s x="s "_arrRef_"("_subs2_")="_name
 . . x x
 . . s name=""
 . s name=name_c q
 QUIT error
 ;
getJSONValue(buff)
 n apos,c,isLiteral,lc,stop,value
 s stop=0,value="",isLiteral=0,lc=""
 f  d  q:stop  q:buff=""
 . s c=$e(buff,1)
 . i value="" d
 . . i c="""" s isLiteral=1,apos="""" q
 . . i c="'" s isLiteral=1,apos="'" q
 . i 'isLiteral,c="[" s stop=1 q
 . i 'isLiteral,c="{" s stop=1 q
 . i c="}" d  q:stop
 . . i isLiteral,lc'=apos q
 . . s stop=1
 . i c="," d  q:stop
 . . i isLiteral,lc'=apos q
 . . s stop=1
 . s buff=$e(buff,2,$l(buff))
 . s value=value_c
 . s lc=c
 i $e(value,1)="'" s value=""""_$e(value,2,$l(value)-1)_""""
 QUIT value
 ;
removeSpaces(string)
 ;
 n c,i,quote,quoted,outString
 ;
 s quoted=0,quote=""
 s outString=""
 f i=1:1:$l(string) d
 . s c=$e(string,i)
 . i $a(c)=9 q
 . i c="""" d
 . . i 'quoted d
 . . . s quoted=1
 . . . s quote=""""
 . . e  d
 . . . i quote="""" d
 . . . . s quoted=0
 . . . . s quote=""
 . i c="'" d
 . . i 'quoted d
 . . . s quoted=1
 . . . s quote="'"
 . . e  d
 . . . i quote="'" d
 . . . . s quoted=0
 . . . . s quote=""
 . i c=" ",'quoted q
 . i quoted,c=":" s c=$c(2)
 . s outString=outString_c
 ;
 QUIT outString
 ;
replace(string,substr,to) ;
 i $zv["GT.M" QUIT $$^%MPIECE(string,substr,to)
 e  QUIT $replace(string,substr,to)
 ;
UTCDateTime(hdate) ;
 ;
 n date
 ;
 i $zv["GT.M" d
 . n dd,mm,time,yy
 . s date=$$CDS^%H(hdate)
 . s dd=$p(date,"/",2)
 . s mm=$p(date,"/",1)
 . s yy=$p(date,"/",3)
 . s time=$$CTS^%H($p(hdate,",",2))
 . s date=yy_"-"_mm_"-"_dd_"T"_time_".000Z"
 else  d
 . s date=$zdatetime(hdate,3,7,2)
 ;
 QUIT date
 ;
epochTime(hdate)
 ;
 n time
 ;
 s time=(hdate*86400)+$p(hdate,",",2)
 s time=time-4070908800
 QUIT time*1000
 ;
now() ;
 ;
 n now
 ;
 s now=$h
 i $zv'["GT.M" s now=$zts
 QUIT now
 ;
convertDateToSeconds(hdate)
 ;
 Q (hdate*86400)+$p(hdate,",",2)
 ;
strx(string) ;
 n i,x
 s x=""
 f i=1:1:$l(string) d
 . s x=x_$a($e(string,i))_" "
 QUIT x
 ;
isValidEmail(email) ;
  ;
  n chk,domain,dupFound,i,name,specialChars
  ;
  ; just a single @ ?
  i $l(email,"@")'=2 QUIT 0
  ; 
  s name=$p(email,"@",1)
  ; missing name?
  i name="" QUIT 0
  ; starts or ends with a letter or number?
  i $e(name,1)'?1AN QUIT 0
  i $e(name,$l(name))'?1AN QUIT 0
  ; duplicated special characters?
  s specialChars=".!#$%&'*+-/=?^_`{|"
  s dupFound=0
  f i=1:1:$l(specialChars) d  q:dupFound
  . n s,ss
  . s s=$e(specialChars,i)
  . s ss=s_s
  . s chk=$$replace^%zmgwebUtils(name,ss,s)
  . i chk'=name s dupFound=1
  i dupFound QUIT 0
  ;
  ; any other character than alphas, numbers or special characters?
  s chk=$tr(name,specialChars,"")
  i chk'?1AN.AN QUIT 0
  ;
  s domain=$p(email,"@",2)
  i $e(domain,1)'?1AN QUIT 0
  i $e(domain,$l(domain))'?1AN QUIT 0
  ; missing top-level domain?
  i $l(domain,".")<2 QUIT 0
  ; missing domain name
  i $p(domain,".",1)="" QUIT 0
  ; missing intermediate domain parts
  i $$replace^%zmgwebUtils(domain,"..",".")'=domain QUIT 0
  ; invalid characters in domain name?
  s chk=$tr(domain,".-","")
  i chk'?1AN.AN QUIT 0
  ; looks like it's formatted OK
  QUIT 1
  ;
incorrectTest(email)
  i $$isValidEmail(email) d
  . w email_" was invalid but passed",!
  e  d
  . w email_" was correctly rejected",!
  QUIT
  ;
correctTest(email)
  i $$isValidEmail(email) d
  . w email_" passed OK",!
  e  d
  . w email_" was OK but failed",!
  QUIT
  ;
emailTests ;
  ;
  d correctTest("john.doe@gmail.com")
  d correctTest("john.doe43@domainsample.co.uk")
  d incorrectTest(".doe@gmail.com")
  d incorrectTest("@domainsample.com")
  d incorrectTest("johndoedomainsample.com")
  d incorrectTest("john.doe@.net")
  d incorrectTest("john.doe43@domainsample")
  d correctTest("john.do%e43@domainsample.com")
  d incorrectTest("john.do%%e43@domainsample.com")
  d incorrectTest("john.do%e43@domainsample..com")
  d incorrectTest("john.do%e43@domainsample.com.")
  d incorrectTest("john.do(e43@domainsample.com")
  d incorrectTest("john.doe43@domai%nsample.com")
  ;
  QUIT
  ;
hashPassword(password) ;
 ;
 n hash
 ;
 i $zv["GT.M" d
 . s hash=$$bcryptHash(password)
 e  d
 . n salt
 . s salt=##class(%SYSTEM.Encryption).GenCryptRand(32)
 . s hash=##class(%SYSTEM.Encryption).PBKDF2(password,20,salt,32,512)
 . s hash=##class(%SYSTEM.Encryption).Base64Encode(salt)_"$32$"_##class(%SYSTEM.Encryption).Base64Encode(hash)
 ;
 QUIT hash
 ;
verifyPassword(password,hash) ;
 ;
 n ok
 ;
 i $zv["GT.M" d
 . s ok=$$bcryptCompare(password,hash)
 e  d
 . n hash2,salt
 . s salt=$p(hash,"$32$",1)
 . s hash=$p(hash,"$32$",2) 
 . s salt=##class(%SYSTEM.Encryption).Base64Decode(salt)
 . s hash2=##class(%SYSTEM.Encryption).PBKDF2(password,20,salt,32,512)
 . s hash2=##class(%SYSTEM.Encryption).Base64Encode(hash2)
 . s ok=(hash=hash2)
 ;
 QUIT ok
 ;
bcryptHash(password) ;
 ;
 n command,filename,hash,io,msg,name,ok,username
 ;
 s io=$io
 s name="bcrypt"
 s username="dummy"
 s filename="/tmp/passwd"_$j
 i $$deleteFile(filename)
 s command="htpasswd -cbBC 10 -i "_filename_" "_username
 open name:(command=command)::"pipe"
 use name w password,! w /EOF
 u name r msg
 close name u io
 s ok=$$openFile(filename)
 h 1 ; needs a second to complete
 u filename r hash
 c filename
 u io
 i $$deleteFile(filename)
 s hash=$p(hash,username_":",2)
 QUIT hash
 ;
bcryptCompare(password,hash) ;
 ;
 n command,filename,io,msg,name,ok,success,username
 ;
 s name="bcrypt"
 s io=$io
 s username="dummy"
 s filename="/tmp/passwd"_$j
 i $$openNewFile(filename)
 u filename w username_":"_hash,! c filename 
 s command="htpasswd -vbC 10 -i "_filename_" "_username_" ; echo $?"
 open name:(command=command)::"pipe"
 u name w password,! w /EOF
 use name read msg,success
 u io 
 close name
 s ok=(success=+0)
 i $$deleteFile(filename)
 QUIT ok
 ;
.



